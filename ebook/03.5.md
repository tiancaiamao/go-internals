# 3.5 分裂栈

Go语言中使用的是栈不是连续的，原因是需要支持goroutine。假如每个goroutine分配固定栈大小并且不能增长，太小则会导致溢出，太大又会浪费空间，无法开许多的goroutine。分段栈的重要意义就在于，可以初始时只给栈分配很小的空间，然后随着使用过程中的需要自动地增长。这就是为什么Go可以开千千万万个goroutine而不会耗尽内存。

## 基本原理
为了便于理解，可以拿操作系统中的分页和Go语言的分裂栈进行类比。在操作系统中，每个进程可以使用整个地址空间，并不会感觉到它使用的内存是一页一页不连续的。其原理就在于操作系统会处理好底层的细节使得上层看起来像是一个逻辑上连续的内存空间。假如进程访问到一个逻辑地址，该地址还没有分配物理内存，则此时会发生缺页中断。操作系统会把发生中断时的上下文保存下来，然后为进程分配一页大小的物理内存，设置好映射关系，最后恢复中断发生前的上下文。进程继续运行时，并不会感觉到缺页中断的整个过程。

分裂栈也是很类似的。当栈的大小不够用时，会触发“中断”，从当前函数进入到Go的运行时库，Go的运行时库会保存此时的函数上下文环境，然后分配一个新的足够大的栈空间，并做一些设置，使得当函数恢复运行时，它会在新分配的栈中继续执行，仿佛整个过程都没发生过一样，这个函数会觉得自己使用的是一块大小“无限”的连续栈空间。

## 实现过程
在研究Go的实现细节之前让我们先自己思考一下应该如何实现。第一步肯定要有某种机制检测到当前栈大小不够用了，这个应该是把当前的栈寄存器SP跟栈的可用栈空间的边界进行比较。能够检测到栈大小不够用，就相当于捕捉到了“中断”。那么第二步要做的，就应该是进入运行时，保存当前的上下文。别陷入如何保存上下文的细节，先假如我们把函数栈增长时的上下文保存好了，那下一步就是分配新的栈空间了，我们可以将分配空间想象成就是调用一下malloc而已。

接下来怎么办呢？如何让函数在新的栈中继续运行，好像没发生过栈分裂一样？可能很多人会卡在这一步了。我们可以把SP直接设置为新栈空间的顶部，然后用JMP指令而不是CALL指令直接跳到保存PC寄存器的位置。这样确实是让函数在新的栈中继续运行了，但是有个问题，函数返回时会出错。因为我们没有设置好函数返回信息，栈增长后，函数在新栈中执行完却无法回到旧的栈了。

为了解决返回的问题，可以装饰一下新栈的顶部，并引入一个辅助函数，使函数在新栈中返回时是返回到这个辅助函数。这个辅助函数做的事情跟前面的过程正好相反，它会将SP设置回旧栈地址，然后JMP到发生“中断”时的PC。这样就可以恢复到整个旧栈的上下文环境了。

函数调用时的内存布局前面小节已经讲过了，依次是：为返回值保留的空间，传入的参数，保存返回地址，然后就是被调函数的栈的。进入函数后的前几条指令并不像普通C语言那样是push %ebp; mov %esp %ebp这样子，这个细节后面再看。

可以想象一下切换到新栈时，栈顶空间大致应该是被装饰成这样子的：

	...       <-新栈空间顶部
	旧栈参数3
	旧栈参数2
	旧栈参数1
	辅助函数地址
	...       <-SP

到新栈以后，函数继续运行，并不会感觉到发生过从旧栈切换到新栈的过程。然后当函数返回时，RET指令会使用辅助函数地址覆盖当前PC。

## 具体细节
Go语言中有个结构体G，用于描述goroutine。这个结构中存了栈基址和stack_guard。对于使用分段栈的函数，每次进入函数后，前几条指令就是先比较SP跟g->stack_guard，检测是否发生溢出。如果栈寄存器超过了stackguard就需要要扩展栈空间。

   所有用于分配栈空间的函数本身不能使用分段栈，引入一个属性来控制函数的生成。当然，还要保证有足够的空间来调用分配函数。编译器在编译阶段加入特殊的标签，链接时对于检测到有这些标签的函数，就会插入特殊指令。扩展完之后，使用新栈之间，需要一些处理。

   基于原栈SP的数据都可以直接复制到新栈中来。对于栈中存的是对象的地址，这样做不会造成问题。对于带参函数，因为参数不能直接搬，编译时要特殊处理．函数使用的参数指针不是基于栈帧的．对于在栈中返回对象的函数，对象必须返回到原栈上。
 
   扩展栈时，函数的返回地址会被修改成一个函数，这个函数会释放分配的栈块，将栈指针重新设置成调用者旧栈块的地址，栈指针等，需要在新栈空间中的某处保存着。

为了加深理解，下面让我们跟踪一下代码，并看看实际生成的汇编吧。首先写一个test.go文件，内容如下：

```go  
	package main

	func main() {
		main()
	}
```

然后生成汇编文件：
```sh
	go tool 6g -S test.go | head -8
```
可以看以输出是：

	0000 (test.go:3) TEXT    main+0(SB),$0-0
	0001 (test.go:3) LOCALS  ,$0
	0002 (test.go:4) CALL    ,main+0(SB)
	0003 (test.go:5) RET     ,

让我们用链接器进行反汇编：

```sh
	go tool 6l -a test.6 | head -10
```

注意观察输出：

	002000	main.main            | (3)	TEXT	main.main+0(SB),$0
	002000	65488b0c25a0080000   | (3)	MOVQ	2208(GS),CX
	002009	483b21               | (3)	CMPQ	SP,(CX)
	00200c	7705                 | (3)	JHI	,2013
	00200e	e84d760100           | (3)	CALL	,19660+runtime.morestack00
	002013	                     | (3)	NOP	,
	002013	                     | (3)	NOP	,
	002013	e8e8ffffff           | (4)	CALL	,2000+main.main
	002018	c3                   | (5)	RET	,

会发现链接器的输出跟编译器的输出是不同的。链接器中插入了一些指令，让我们好好看一下在CALL main.main之前插入的这些指令。(GS)取到的是结构体G的第一个域，也就是g->stackguard，将它赋值给CX。然后与SP进行比较，如果SP大于g->stackguard了，则会调用runtime.morestack00。插入的指令作用就是检测栈是否溢出。编译生成的汇编是没有插入指令的，说明分裂栈是在链接过程中实现的，而不是在编译过程中实现的。编译还是按正常方式编译的，而链接时会在每个函数前面插入检测栈溢出的指令。哇，跟go和defer关键字一样的精妙！不必在编译层做修改，没有打破原有的协议。

不过也不绝对。如果你读源代码，可能会发现"#pragma textflag 7"，或者在汇编函数中看到"TEXT reuntime.exit(SB),7,$0"，这种函数就是不会检测栈溢出的。这个是编译标记，汇编或者编译器中的标记7让链接器不要插入栈溢出检测的指令。

注意到它调用了runtime.morestack00。在asm_amd64.s中还有runtime.morestack10，runtime.morestack01，runtime.morestack11等好几个类似runtime.morestack00的函数，其实它们都是调用到runtime.morestack的。在stack.h中有说明为什么这么做，对不同大小的函数采用了不同的策略。每个goroutine的g->stackguard设置成指向栈底上面StackGuard的位置。每个函数会通过比较栈指针和g->stackguard检测栈溢出。对于那些只有很小栈帧的函数，为了减小检测的指令数目，可以允许栈越过stack guard下方StackSmall字节以内。使用较大栈的函数则总是会检测栈溢出并调用morestack。

	guard = g->stackguard
	frame = function's stack frame size
	argsize = size of function arguments (call + return)

	stack frame size <= StackSmall:
		CMPQ guard, SP
		JHI 3(PC)
		MOVQ m->morearg, $(argsize << 32)
		CALL morestack(SB)

	stack frame size > StackSmall but < StackBig
		LEAQ (frame-StackSmall)(SP), R0
		CMPQ guard, R0
		JHI 3(PC)
		MOVQ m->morearg, $(argsize << 32)
		CALL morestack(SB)

	stack frame size >= StackBig:
		MOVQ m->morearg, $((argsize << 32) | frame)
		CALL morestack(SB)

接着看一下runtime.morestack的实现。源代码在runtime/asm_amd64.s中。有两点信息要注意：第一点是morestack将一些信息存在struct M中，这些信息包括当前栈桢，参数，当前函数调用，函数返回地址，（两个返回地址，一个是runtime.morestack的函数地址，一个是f的返回地址）。通过这些信息可以把新栈和旧栈链起来。下面是具体的函数：

```asm
	TEXT runtime·morestack(SB),7,$0
		// 调度器的栈m->g0是无法增长的，下面几条指令是确保当前goroutine不是m->g0
		MOVQ	m_g0(BX), SI  //这里g0(BX)就是m->g0
		CMPQ	g(CX), SI  //g(CX)就是g
		JNE	2(PC)
		INT	$3  //INT $3是留给调试器的中断号，正常执行应该不会运行到这里

		MOVQ	DX, m_cret(BX) //BX是存的m结构体指针，m_cret(Bx)是m->cret，C的返回值。DX存的是

		// f是调用runtime.morestack的函数
		// 将m->morebuf设置成f的调用者
		MOVQ	8(SP), AX	// f's caller's PC
		MOVQ	AX, (m_morebuf+gobuf_pc)(BX) //将f的调用者的PC记录到m->morebuf.pc
		LEAQ	16(SP), AX	// f's caller's SP
		MOVQ	AX, (m_morebuf+gobuf_sp)(BX) //将f的调用者的SP记录到m->morebuf.sp
		MOVQ	AX, m_moreargp(BX) //morestack的参数
		get_tls(CX)
		MOVQ	g(CX), SI //当前的g -> SI
		MOVQ	SI, (m_morebuf+gobuf_g)(BX) //将当前的g记录到m->morebuf.g

		// Set m->morepc to f's PC.
		MOVQ	0(SP), AX 
		MOVQ	AX, m_morepc(BX) //将m->morepc设置为f的PC

		// Call newstack on m->g0's stack.
		// 切换到g0的栈，在g0的栈中调用newstack函数
		MOVQ	m_g0(BX), BP
		MOVQ	BP, g(CX) //将m->g0切换成当前g
		MOVQ	(g_sched+gobuf_sp)(BP), SP //将m->g0->g_sched.gobuf_sp设置成当前的SP
		CALL	runtime·newstack(SB) //调用newstack
		MOVQ	$0, 0x1003	// crash if newstack returns
		RET
```

这段源代码中有函数f和函数f的调用者，要注意将两者区分开来。函数f指的是是调用morestack的函数，但不是前面的morestack00，morestack01等，而是真正触发栈增长的函数。虽然控制流是从f到morestack00再到morestack的，但是morestack00到morestack的方式并不是调用的CALL，而是直接JMP过来的。所以0(SP)是保存的f的返回地址。为什么8(SP)会是f的调用者的返回地址呢？因为进入函数后最前几条指令就是检测栈溢出的，它还没来得及使用自己的栈空间，它的栈是空的，所以此时8(SP)就是f的调用者的返回地址，是CALL指令时推进来的。

然后需要注意的就是newstack是切换到m->g0的栈中去调用的。m->g0是调度器栈，go的运行时库调度器使用的都是m->g0。

整个newstack的源代码都是很好读的，newstack中有很大一部分跟reflect相关的代码，现在先不管它。newstack做的事情很容易看明白：分配新栈空间，在该空间顶部初始化一个struct Stktop的东西，然后以很奇特的方式退出。

```go
	//分配新空间
	stk = runtime·stackalloc(framesize);
	top = (Stktop*)(stk+framesize-sizeof(*top)); 
	free = framesize;
	//将一些重要信息从结构体g中移到栈顶Stktop中
	top->stackbase = (byte*)gp->stackbase;
	top->stackguard = (byte*)gp->stackguard; 
	top->gobuf = m->morebuf; //只是借助m结构体临时传了一下参数，morebuf中记录的是栈空间不够的那个函数的pc,sp,g
	top->argp = m->moreargp; //参数
	top->argsize = argsize; //参数大小
	top->free = free; //可用空间大小
	//装饰栈顶的参数区域
	sp = (byte*)top; //top结构体中存的是一些重要信息，结构体下方就是可用的栈空间起始位置了。
	if(argsize > 0) { //将参数移过来
	sp -= argsize;
	dst = (uintptr*)sp;
	dstend = dst + argsize/sizeof(*dst);
	src = (uintptr*)top->argp;
	while(dst < dstend)
	*dst++ = *src++;
	}
	//跳转，伪装成好像是从m->morepc中调用lessstack函数的状态
	label.sp = (uintptr)sp;
	label.pc = (byte*)runtime·lessstack;
	label.g = m->curg;
	runtime·gogocall(&label, m->morepc, m->cret); //gogocall相当于一个直接的jmp，不是按函数协议跳转的
```

要稍微注意看的是装饰栈顶的部分代码，此时的内存布局，上方是一个Stktop结构体，下方就是栈，并且参数已经进栈了。接下来调用gogocall，这个函数的三个参数分别是Gobuf，一个函数地址，以及一个返回值。该函数会跳转到第二个参数标识的函数中，但返回时会返回到Gobuf保存的上下文，而不是调用gogocall的函数。

```asm
	// void gogocall(Gobuf*, void (*fn)(void), uintptr r0)
	// 跳转到fn，从fn返回时会返回到Gobuf保存的状态
	TEXT runtime·gogocall(SB), 7, $0
	MOVQ	24(SP), DX	// gogocall函数的第3个参数，实际上是m->cret
	MOVQ	16(SP), AX		// 第2个参数，fn
	MOVQ	8(SP), BX		// 第1个参数，是一个gobuf结构体指针
	MOVQ	gobuf_g(BX), DI	//gobuf->g
	get_tls(CX)
	MOVQ	DI, g(CX)	//设置回之前的g...现在这个函数是在m->g0中调用的
	MOVQ	0(DI), CX	// make sure g != nil
	MOVQ	gobuf_sp(BX), SP	// 这下子SP才真正的切换到新栈中了
	MOVQ	gobuf_pc(BX), BX
	PUSHQ	BX	// 在刚进入函数时是SP指向保存的返回地址的。这里正好是这样，当函数返回时就会返回到BX了，也就是lessstack
	JMP	AX
	POPQ	BX	// not reached
```

runtime.lessstack比较简单，它其实就是切换到m->g0栈之后调用runtime.oldstack函数。这时之前保存的那个Stktop结构体是时候发挥作用了，从上面可以找到旧栈空间的SP和PC等信息，通过runtime.gogo跳转过去，整个过程就完成了。

```go
	gp = m->curg; //当前g
	top = (Stktop*)gp->stackbase; //取得Stktop结构体
	label = top->gobuf; //从结构体中取出Gobuf
	runtime·gogo(&label, cret); //通过Gobuf恢复上下文
```

整个分裂栈的细节终于分析完了，函数调用链是f->morestack00->morestack->newstack->f在新栈中->f调用的函数...->lessstack->f。

再重复一遍整个过程：
1. 使用分段栈的函数头几个指令检测%esp和stackguard，调用于runtime.morestack
2. runtime.more函数的主要功能是保存当前的栈的一些信息．然后转换成调试器的栈了调用runtime.newstack
3. runtime.newstack函数的主要功能是分配空间，装饰此空间，将旧的frame和arg弄到新空间
4. 使用gogocall的方式切换到新分配的栈，gogocall使用的JMP返回到被中断的函数
5. 继续执行遇到RET指令时会返回到runtime.less，less做的事情跟more相反，它要准备好从newstack到old　stack
  整个过程有点像一次中断，中断处理时保存当时的现场，弄个新的栈，中断恢复时恢复到新栈中运行，运行到return时又要从runtime.less走回去

## links
 * [目录](<preface.md>)
 * 上一节: [defer关键字](<03.4.md>)
 * 下一章: [Go语言程序初始化过程](<04.0.md>)
